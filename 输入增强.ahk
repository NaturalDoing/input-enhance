#Requires AutoHotkey v2.0

; 功能：
; 1. `Win + CapsLock` 切换大小写
; 2. `CapsLock` 单击等于 `Esc` 单击
; 3. `CapsLock` 和其他键组合等于 `RCtrl` 和其他键组合
#CapsLock::CapsLock
CapsLock::RCtrl
~CapsLock up::{
    Send "{RCtrl up}"
    if A_PriorKey == "CapsLock"{
        Send "{Esc}"
    }
}

`;::`;

; 功能：
; 1. `; + j` 进入编辑模式
; 2. `o` 键退出编辑模式
; 3. 在编辑模式中 `i j k l` 映射成 `↑ ← ↓ →` 方向键，`h ;` 映射成 `Home End`
; 4. 在编辑模式中 `m` 映射成 `AppsKey`
编辑模式 := false
`; & j::{
    global 编辑模式
    编辑模式 := true
}

#HotIf 编辑模式

o::{
    global 编辑模式
    编辑模式 := false
}

tab::Alt
h::Home
`;::End
i::Up
j::Left
k::Down
l::Right
m::AppsKey

#HotIf !编辑模式

; 功能：
; 1. `tab` 单击是 `tab`
; 2. `tab + m` 实现右键菜单键
; 3. `tab + i j k l` 映射 `↑ ← ↓ →` 方向键，`tab + h` 映射成 `Home`，`tab + ;` 映射成 `End`
tab::tab
tab & h::Home
tab & `;::End
tab & i::Up
tab & j::Left
tab & k::Down
tab & l::Right
tab & m::AppsKey

#HotIf


双击间隔 := 400 ; 双击时间间隔（毫秒）
第一次按键时间 := 0
第一次按键符号 := ""


DetectHiddenWindows(true) ; 设置脚本可以 "看见" 隐藏的窗口.
是中文状态() {
    活动窗口句柄id := WinGetID("A") ; 获取窗口句柄id
    活动窗口的输入法窗口句柄id := DllCall("imm32\ImmGetDefaultIMEWnd", "Uint", 活动窗口句柄id)
    输入法状态 := SendMessage(0x283, 0x005, 0, , "ahk_id" 活动窗口的输入法窗口句柄id) ; 发送 WM_IME_CONTROL 消息到窗口来获取活动窗口的输入法状态。
    return 输入法状态 ; 1 表示中文， 0 表示英文
}

恢复按键虚拟状态与物理状态一致(输入键){
    ; Shift 键物理状态是否按下和虚拟状态不一致时恢复成物理状态。
    if (GetKeyState(输入键, "P")) {
        Send("{" 输入键 " down}")
    } else {
        Send("{" 输入键 " up}")
    }
}

中文状态双击输出英文符号(输入按键, 删除次数, 是上档键, *) {
    global 第一次按键时间
    global 第一次按键符号

    当前时间 := A_TickCount

    ; 检查是否是双击
    if (当前时间 - 第一次按键时间 < 双击间隔 && 输入按键 == 第一次按键符号) {
        if (是中文状态()) {
            Sleep 60
            Send("{Backspace " 删除次数 "}")
            if (是上档键) {
                恢复按键虚拟状态与物理状态一致("Shift")
            }
            SendText(输入按键)
        }
        第一次按键时间 := 0
        第一次按键符号 := ""
    } else {
        第一次按键时间 := 当前时间
        第一次按键符号 := 输入按键
    }
}

中文状态双击输出英文符号_有成对符号的左边键(右移次数, 删除次数, 左下档键, 右下档键, 左上档键, 右上档键) {
    global 第一次按键时间
    global 第一次按键符号

    当前时间 := A_TickCount

    ; 检查是否是双击
    if (当前时间 - 第一次按键时间 < 双击间隔 && (左下档键 == 第一次按键符号 || 左上档键 == 第一次按键符号)) {
        if (是中文状态()) {
            Sleep 60
            Send("{Right " 右移次数 "}")
            Send("{Backspace " 删除次数 "}")
            if (第一次按键符号 == 左下档键) {
                 if (左下档键 == ",") {
                    SendText(左下档键)
                } else{
                    SendText(左下档键 . 右下档键)
                    Send("{Left}")
                }
            } else if (第一次按键符号 == 左上档键) {
                 if (左下档键 == ",") {
                    Send("{Right}")
                    Send("{Backspace}")
                }
                SendText(左上档键 . 右上档键)
                Send("{Left}")
                恢复按键虚拟状态与物理状态一致("Shift")
            }
        }
        第一次按键时间 := 0
        第一次按键符号 := ""
    } else {
        第一次按键时间 := 当前时间
        第一次按键符号 := 左下档键
    }
}

记录成对符号左上档按键第一次按键时间(输入按键) {
    global 第一次按键时间
    global 第一次按键符号

    当前时间 := A_TickCount

    ; 第一个按键记录时间
    if (!(当前时间 - 第一次按键时间 < 双击间隔 && 输入按键 == 第一次按键符号)) {
        第一次按键时间 := 当前时间
        第一次按键符号 := 输入按键
    }
}

; 功能:
; 中文状态下双击输出英文符号

#HotIf !WinActive("ahk_exe Obsidian.exe")

; 非成对符号下档键和成对符号右下档键
~`:: {
    中文状态双击输出英文符号("``", 2, false)
}
~]:: {
    中文状态双击输出英文符号("]", 2, false)
}
~\:: {
    中文状态双击输出英文符号("\", 2, false)
}
#HotIf !编辑模式
`;:: {
    Send(";")
    中文状态双击输出英文符号(";", 2, false)
}
#HotIf
~.:: {
    中文状态双击输出英文符号(".", 2, false)
}
~/:: {
    中文状态双击输出英文符号("/", 2, false)
}

; 非成对符号上档键和成对符号右上档键
~!:: {
    中文状态双击输出英文符号("!", 2, true)
}
~$:: {
    中文状态双击输出英文符号("$", 2, true)
}
~^:: {
    中文状态双击输出英文符号("^", 4, true)
}
~):: {
    中文状态双击输出英文符号(")", 2, true)
}
~_:: {
    中文状态双击输出英文符号("_", 4, true)
}
~}:: {
    中文状态双击输出英文符号("}", 2, true)
}
+~;:: {
    中文状态双击输出英文符号(":", 2, true)
}
~>:: {
    中文状态双击输出英文符号(">", 2, true)
}
~?:: {
    中文状态双击输出英文符号("?", 2, true)
}

; 成对符号左上档键
~(:: {
    记录成对符号左上档按键第一次按键时间("(")
}
~{:: {
    记录成对符号左上档按键第一次按键时间("{")
}
~":: {
    记录成对符号左上档按键第一次按键时间('"')
}
~<:: {
    记录成对符号左上档按键第一次按键时间("<")
}

; 成对符号左下档键
~9:: {
    中文状态双击输出英文符号_有成对符号的左边键("1", "3", "9", "0", "(", ")")
}
~[:: {
    中文状态双击输出英文符号_有成对符号的左边键("2", "4", "[", "]", "{", "}")
}
~':: {
    中文状态双击输出英文符号_有成对符号的左边键("2", "4", "'", "'", '"', '"')
}
~,:: {
    中文状态双击输出英文符号_有成对符号的左边键("0", "2", ",", ".", "<", ">")
}

#HotIf

#Requires AutoHotkey v2.0

; 功能：
; 1. `Win + CapsLock` 切换大小写
; 2. `CapsLock` 单击等于 `Esc` 单击
; 3. `CapsLock` 和其他键组合等于 `RCtrl` 和其他键组合
#CapsLock::CapsLock
CapsLock::RCtrl
~CapsLock up:: {
    Send "{RCtrl up}"
    if A_PriorKey == "CapsLock" {
        Send "{Esc}"
    }
}

; `CapsLock + Tab` 组合：若仅按下 `Tab` 后释放，则发送 `Ctrl + Tab` 以实现标签页切换；若与其他键组合，则保持 `Tab` 模式
>^tab up:: {
    if A_PriorKey == "Tab" {
        Send "^{tab}"
    }
}

`;::`;

; 功能：
; 1. `; + j` 进入编辑模式
; 2. `o` 键退出编辑模式
; 3. 在编辑模式中 `i j k l` 映射成 `↑ ← ↓ →` 方向键，`h ;` 映射成 `Home End`
; 4. 在编辑模式中 `m` 映射成 `AppsKey`
编辑模式 := false
`; & j:: {
    global 编辑模式
    编辑模式 := true
}

#HotIf 编辑模式

o:: {
    global 编辑模式
    编辑模式 := false
}

tab::Alt
h::Home
`;::End
i::Up
j::Left
k::Down
l::Right
m::AppsKey

#HotIf !编辑模式

; 功能：
; 1. `tab` 单击是 `tab`
; 2. `tab + m` 实现右键菜单键
; 3. `tab + i j k l` 映射 `↑ ← ↓ →` 方向键，`tab + h` 映射成 `Home`，`tab + ;` 映射成 `End`
tab::tab
tab & h::Home
tab & `;::End
tab & i::Up
tab & j::Left
tab & k::Down
tab & l::Right
tab & m::AppsKey

#HotIf

双击间隔 := 400 ; 双击时间间隔（毫秒）
第一次按键时间 := 0
第一次按键符号 := ""
上次双击按键时间 := 0
上次双击按键符号 := ""

DetectHiddenWindows(true) ; 设置脚本可以 "看见" 隐藏的窗口

获取输入法状态(是否设置为中文 := false) {
    活动窗口句柄_ID := WinGetID("A") ; 获取窗口句柄id
    活动窗口输入法窗口句柄_ID := DllCall("imm32\ImmGetDefaultIMEWnd", "Uint", 活动窗口句柄_ID)
    输入法状态 := SendMessage(0x283, 0x005, 0, , "ahk_id" 活动窗口输入法窗口句柄_ID) ; 发送 WM_IME_CONTROL 消息到窗口来获取活动窗口的输入法状态。

    if (是否设置为中文 && 输入法状态 == 0) {
        SendMessage(0x283, 0x006, 1, , "ahk_id" 活动窗口输入法窗口句柄_ID) ; 发送 WM_IME_CONTROL 消息，wParam=0x006(IMM_SET_OPEN_STATUS)，lParam=1 打开中文输入法
    }

    return 输入法状态 ; 1 表示中文， 0 表示英文
}

同步按键状态(输入键) {
    ; Shift 键物理状态是否按下和虚拟状态不一致时恢复成物理状态。
    if (GetKeyState(输入键, "P")) {
        Send("{" 输入键 " down}")
    } else {
        Send("{" 输入键 " up}")
    }
}

中文双击替换成英文_非成对符号(输入按键, 删除次数, 是上档键, *) {
    global 第一次按键时间
    global 第一次按键符号
    global 上次双击按键时间
    global 上次双击按键符号

    当前时间 := A_TickCount

    ; 检查是否是双击
    if (当前时间 - 第一次按键时间 < 双击间隔 && 输入按键 == 第一次按键符号) {
        if (获取输入法状态()) {
            Sleep 60 ; 等待 60 毫秒，确保字符已经输入完成
            if (是上档键) {
                ; 删除已输入的字符
                Send("{Shift up}")
                Send("{Backspace " 删除次数 "}")
                Send("{Shift}")

                同步按键状态("Shift")

                ; 等待 200 毫秒，确保输入法状态是中文状态
                SetTimer(
                    (*) => 获取输入法状态(true),
                    -200
                )
            } else {
                ; 删除已输入的字符
                Send("{Blind}{Backspace " 删除次数 "}")
            }

            ; 输入英文符号
            SendText(输入按键)
        }

        上次双击按键时间 := 当前时间
        上次双击按键符号 := 第一次按键符号
        第一次按键时间 := 0
        第一次按键符号 := ""
    } else {
        第一次按键时间 := 当前时间
        第一次按键符号 := 输入按键

        ; 三击处理
        是固定时间内三击 := 当前时间 - 上次双击按键时间 < 双击间隔
        if (上次双击按键符号 == 第一次按键符号 && 是固定时间内三击 && 获取输入法状态()) {
            if (第一次按键符号 == "]") {
                中文三击替换成英文_成对符号(第一次按键符号, 0, 1)
            }
        }
        
        上次双击按键时间 := 0
        上次双击按键符号 := ""
    }
}

中文双击替换成英文_成对符号(右移次数_双击, 删除次数_双击, 右移次数_三击, 删除次数_三击, 左下档键, 右下档键, 左上档键, 右上档键) {
    global 第一次按键时间
    global 第一次按键符号
    global 上次双击按键时间
    global 上次双击按键符号

    当前时间 := A_TickCount

    ; 检查是否是双击
    if (当前时间 - 第一次按键时间 < 双击间隔 && (左下档键 == 第一次按键符号 || 左上档键 == 第一次按键符号)) {
        ; 处理中文输入状态下的双击逻辑：删除已输入的字符，发送正确的标点符号组合
        if (获取输入法状态()) {
            Sleep 60 ; 等待 60 毫秒，确保字符已经输入完成

            ; 删除已输入的字符
            Send("{Right " 右移次数_双击 "}")
            Send("{Backspace " 删除次数_双击 "}")

            if (第一次按键符号 == 左下档键) {
                if (左下档键 == ",") {
                    SendText(左下档键)
                } else {
                    SendText(左下档键 . 右下档键)
                    Send("{Left}") ; 移动光标到成对符号中间
                }
            } else if (第一次按键符号 == 左上档键) {
                ; 删除已输入的字符
                if (左下档键 == ",") {
                    Send("{Right}")
                    Send("{Backspace}")
                }

                SendText(左上档键 . 右上档键)
                Send("{Left}") ; 移动光标到成对符号中间

                同步按键状态("Shift")

                ; 等待 200 毫秒，确保输入法状态是中文状态
                SetTimer(
                    (*) => 获取输入法状态(true),
                    -200
                )
            }
        }

        上次双击按键时间 := 当前时间
        上次双击按键符号 := 第一次按键符号
        第一次按键时间 := 0
        第一次按键符号 := ""
    } else {
        第一次按键时间 := 当前时间
        第一次按键符号 := 左下档键

        ; 三击处理
        是固定时间内三击 := 当前时间 - 上次双击按键时间 < 双击间隔
        if (上次双击按键符号 == 第一次按键符号 && 是固定时间内三击 && 获取输入法状态()) {
            if (第一次按键符号 == "``" || 第一次按键符号 == "[") {
                中文三击替换成英文_成对符号(第一次按键符号, 右移次数_三击, 删除次数_三击)
            }
        }

        上次双击按键时间 := 0
        上次双击按键符号 := ""
    }
}

中文三击替换成英文_成对符号(输入符号, 右移次数_三击, 删除次数_三击) {
    if (输入符号 == "``") {
        ; `键三击输出代码块格式的符号(```...```)
        Send("{Right " 右移次数_三击 "}")
        Send("{Backspace " 删除次数_三击 "}")
        Send("{Right}")
        SendText(上次双击按键符号)

        if (!WinActive("ahk_exe Obsidian.exe")) {
            SendInput("+{Enter}")
            SendText(上次双击按键符号 . 上次双击按键符号 . 上次双击按键符号)
            Send("{Left 4}")
        }
    } else if (输入符号 == "[") {
        Send("{Right " 右移次数_三击 "}")
        Send("{Backspace " 删除次数_三击 "}")

        SendText("[" . "]")
        Send("{Left}") ; 移动光标到成对符号中间
    } else if (输入符号 == "]") {
        Send("{Right " 右移次数_三击 "}")
        Send("{Backspace " 删除次数_三击 "}")

        SendText("]")
    }
}

记录首次按键时间(输入按键) {
    global 第一次按键时间
    global 第一次按键符号

    当前时间 := A_TickCount

    ; 第一个按键记录时间
    if (!(当前时间 - 第一次按键时间 < 双击间隔 && 输入按键 == 第一次按键符号)) {
        第一次按键时间 := 当前时间
        第一次按键符号 := 输入按键
    }
}

; 功能:
; 中文状态下双击输出英文符号

; 非成对符号下档键和成对符号右下档键
~]:: {
    中文双击替换成英文_非成对符号("]", 2, false)
}
~\:: {
    中文双击替换成英文_非成对符号("\", 2, false)
}
#HotIf !编辑模式
`;:: {
    Send(";")
    中文双击替换成英文_非成对符号(";", 2, false)
}
#HotIf
~.:: {
    中文双击替换成英文_非成对符号(".", 2, false)
}
~/:: {
    中文双击替换成英文_非成对符号("/", 2, false)
}

; 非成对符号上档键和成对符号右上档键
~!:: {
    中文双击替换成英文_非成对符号("!", 2, true)
}
~$:: {
    中文双击替换成英文_非成对符号("$", 2, true)
}
~^:: {
    中文双击替换成英文_非成对符号("^", 4, true)
}
~):: {
    中文双击替换成英文_非成对符号(")", 2, true)
}
~_:: {
    中文双击替换成英文_非成对符号("_", 4, true)
}
~}:: {
    中文双击替换成英文_非成对符号("}", 2, true)
}
+~;:: {
    中文双击替换成英文_非成对符号(":", 2, true)
}
~>:: {
    中文双击替换成英文_非成对符号(">", 2, true)
}
~?:: {
    中文双击替换成英文_非成对符号("?", 2, true)
}

; 成对符号左上档键
~(:: {
    记录首次按键时间("(")
}
~{:: {
    记录首次按键时间("{")
}
~":: {
    记录首次按键时间('"')
}
~<:: {
    记录首次按键时间("<")
}

; 成对符号左下档键
~9:: {
    中文双击替换成英文_成对符号("1", "3", "1", "2", "9", "0", "(", ")")
}
~[:: {
    中文双击替换成英文_成对符号("2", "4", "1", "2", "[", "]", "{", "}")
}
~':: {
    中文双击替换成英文_成对符号("2", "4", "1", "2", "'", "'", '"', '"')
}
~,:: {
    中文双击替换成英文_成对符号("0", "2", "1", "2", ",", ".", "<", ">")
}
~`:: {
    中文双击替换成英文_成对符号("0", "2", "0", "1", "``", "``", '``', '``')
}
